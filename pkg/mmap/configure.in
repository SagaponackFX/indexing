AC_INIT(mmap, 0.6-8)

AC_CONFIG_HEADERS([src/config.h])

# Search for endian.h and automatically enable
# endianness support if found. Endianness can
# be disabled by passing the --disable-endianness
# configuration option.
AC_CHECK_HEADER([endian.h],
                [HAVE_ENDIAN_H="yes"],
                [HAVE_ENDIAN_H="no"])

AC_ARG_ENABLE([endianness],
              [  --disable-endianness   Disable endianness support],
              [case "${enableval}" in
                 yes | no ) WITH_ENDIANNESS="${enableval}" ;;
                 *        ) AC_MSG_ERROR(bad value ${enableval} for --disable-endianness) ;;
               esac],
              [WITH_ENDIANNESS="$HAVE_ENDIAN_H"])

if test "$WITH_ENDIANNESS" = "yes"; then
    AC_DEFINE([WITH_ENDIANNESS], [1],
        [Define to 1 if you have the <endian.h> header file and wish to compile with endianness support.])
fi

AC_PROG_CC
AC_FUNC_MMAP
AC_CHECK_ALIGNOF(char)
AC_CHECK_ALIGNOF(int)
AC_CHECK_ALIGNOF(short)
AC_CHECK_ALIGNOF(long)
AC_CHECK_ALIGNOF(double)
AC_CHECK_FUNCS(madvise)
AC_OUTPUT
